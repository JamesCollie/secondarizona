'++LotusScript Development Environment:2:5:(Options):0:66
Option Public
Option Declare
'this is mikes remark 27Apr12 13:26

Use "General"

'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Sub Initialize
Declare Function AuditingID
Declare Function HealthSpaceID As String
Declare Function ViolationsID
Declare Function ViolationMasterSetting(fieldname As String)As String
Declare Function FavouriteServer As String
Declare Function UserGuideID
Declare Function WaterSamplesID
Declare Sub Check_ConfigCenter '( Source As NotesUIDatabase )
Declare Function GetDBID( ID As String ) As String
Declare Function TempVendID As String
Declare Function BugReportingID
Declare Function DBDesign() As String
Declare Sub Check_TempVendor
Declare Function GetDependantDatabase( SettingsField As String, DependantDBName As String ) As NotesDatabase
Declare Private Function SkipGetReplicaQuestion( Byval Typ As String, prettype As String ) As Boolean
Declare Private Function age( Byval t As Double ) As String
Declare Private Sub RememberGetReplicaAnswer( Byval Typ As String )

'++LotusScript Development Environment:2:5:(Declarations):0:2
Dim Databases_Servers_Enabled As Boolean
Dim Databases_TempVendor_Enabled As Boolean 


'++LotusScript Development Environment:2:2:Initialize:1:10
Sub Initialize
	Dim session As New NotesSession
	Dim HealthSpace As NotesDatabase
	
	Set HealthSpace = session.CurrentDatabase
	If HSGetProfileDocument(HealthSpace, "GlobalSettings") Is Nothing Or HSGetProfileDocument(HealthSpace, "MasterSettings") Is Nothing Then
		Msgbox("It appears the database '" & HealthSpace.Title & "' is not fully initalized. Please wait until the database has finished replicating.")
	End If
End Sub

'++LotusScript Development Environment:2:1:AuditingID:1:8
Function AuditingID
	AuditingID = GetDBID( "Auditing" )
End Function

'++LotusScript Development Environment:2:1:HealthSpaceID:1:8
Function HealthSpaceID As String
	HealthSpaceID = GetDBID( "HealthSpace" )
End Function

'++LotusScript Development Environment:2:1:ViolationsID:1:8
Function ViolationsID
	ViolationsID = GetDBID( "Violations" )
End Function

'++LotusScript Development Environment:2:1:ViolationMasterSetting:1:8
Function ViolationMasterSetting(fieldname As String)As String
	'returns a setting from the violations master settings doc
	
	'grab master settings
	'Set variables From master settings In the violations db
	
	Dim session As New NotesSession
	Dim ViolDB As NotesDatabase
	Dim ViolMasterSettings As NotesDocument
	Dim idItem As NotesItem	
	Dim server As String
	
	'Get the violatins db In the mastersettings doc
	If Instr(1,DBDesign, "Violations", 5) > 0 Or DBDesign = "" Then
		Set ViolDB = session.Currentdatabase
	Else
		server = session.Currentdatabase.Server
		Set ViolDB = New NotesDatabase( "", "" )
		If Not ViolDB.Openbyreplicaid( Server, ViolationsID ) Then
			If server = "" Then server = "Local"
			Msgbox "Unable to open Violations database on " & server
		End If
	End If
	
	Set ViolMasterSettings = HSGetProfileDocument(ViolDB, "MasterSettings")
	
	If ViolMasterSettings.Hasitem(fieldname) Then
		Set idItem = ViolMasterSettings.GetFirstItem( fieldname )
		ViolationMasterSetting = idItem.Values(0)
	End If
	
End Function

'++LotusScript Development Environment:2:1:FavouriteServer:1:8
Function FavouriteServer As String
	Dim Session As New NotesSession
	Dim MasterSettings As NotesDocument
	Dim idItem As NotesItem
	
	Set MasterSettings = HSGetProfileDocument(Session.CurrentDatabase, "MasterSettings" )
	Set idItem = MasterSettings.GetFirstItem( "ViolationsServer" )
	If idItem Is Nothing Then
		FavouriteServer = ""
	Else
		FavouriteServer = idItem.Values(0)
	End If
End Function

'++LotusScript Development Environment:2:1:UserGuideID:1:8
Function UserGuideID
	UserGuideID = GetDBID( "UserGuide" )
End Function

'++LotusScript Development Environment:2:1:WaterSamplesID:1:8
Function WaterSamplesID
	WaterSamplesId = GetDBID( "WaterSamples" )
End Function

'++LotusScript Development Environment:2:2:Check_ConfigCenter:1:8
Sub Check_ConfigCenter '( Source As NotesUIDatabase )
	Dim Workspace As New NotesUIWorkspace
	Dim Session As New NotesSession
	Dim HealthSpace As NotesDatabase
	Dim ConfigCenter As New NotesDatabase( "", "" )
	Dim MasterSettings As NotesDocument
	Dim Server As String
	Dim Msg As String
	Dim globaldocconfigcenter As NotesDocument
	Dim globaldocehslive As NotesDocument
	Dim FieldNames(12) As String
	Dim SourceField As NotesItem
	Dim DestField As NotesItem
	Dim changed As Variant
	Dim livedocumentidview As NotesView
	Dim configprocontactview As NotesView
	Dim lookupdoc As NotesDocument
	Dim procontactdoc As NotesDocument
	Dim procontactcollection As NotesDocumentCollection
	Dim localityitem As NotesItem
	Dim newprodoc As NotesDocument
	Dim mainprocontactview As NotesView
	Dim ConfigItem As NotesItem
	Dim LocalItem As NotesItem
	Dim bSave As Variant 
	
	Set HealthSpace = Session.CurrentDatabase
	'don't bother for reader access
	If HealthSpace.CurrentAccessLevel <> ACLLEVEL_READER Then
		
		If Not ConfigCenter.OpenByReplicaID( HealthSpace.Server, ViolationsID ) Then
			Set MasterSettings = HSGetProfileDocument(HealthSpace, "MasterSettings" )
			Server = MasterSettings.ViolationsServer(0) 'used no matter which answer is given to the prompt
			If HealthSpace.Server = "" Then
				Msg = "A Local replica of Configuration Center "
			Else
				Msg = "Configuration Center on " & HealthSpace.Server
			End If
			Msg = Msg & " could not be found. Would you like to get a new replica from " & Server & "?"
			'not found, can we go to server???
			If Workspace.Prompt( PROMPT_YESNO, "Create New Replica", Msg ) Then
				'user said yes
				Dim MasterConfigCenter As New NotesDatabase( "", "" )
				If MasterConfigCenter.OpenByReplicaID( Server, ViolationsID ) Then
					'create new local replica
					Set ConfigCenter = MasterConfigCenter.CreateReplica( HealthSpace.Server, MasterConfigCenter.FilePath )
					'create workspace icon
					If ConfigCenter.FilePath <> "" Then Call Workspace.AddDatabase( "", ConfigCenter.FilePath )
				Else
					'SHUT THEM DOWN!
					Msgbox "Unable to locate Configuration Center on " & Server & ". This database will be unable to function correctly", 48, "Program error"
				End If
			Else 'they said No
				If Not ConfigCenter.OpenByReplicaID( Server, ViolationsID ) Then
					'Rats! not on server, or no server available
					'SHUT THEM DOWN!
					Msgbox "Configuration Center not found. This database will be unable to function correctly", 48, "Program error"
				Else
					'continuing with config center opened on server
				End If
			End If 'create replica?
		End If 'exists, all is fine
		
		If Not ConfigCenter Is Nothing Then
			
			'sewage application settings
			FieldNames(0) = "SewagePermitConditions"
			FieldNames(1) = "SewageSystemTypes"
			FieldNames(2) = "SewagePermitComments"
			'admin denial settings
			FieldNames(3) = "AdminDenialReasons"
			FieldNames(4) = "AdminDenialAOSEReasons"
			'sewage construction inspection settings
			FieldNames(5) = "SewerLineMaterial"
			FieldNames(6) = "SepticTankMaterial"
			FieldNames(7) = "MakeandModel"
			FieldNames(8) = "ConveyanceLineMaterial"
			FieldNames(9) = "DistributionSystemMaterial"
			FieldNames(10) = "HeaderLinesMaterial"
			FieldNames(11) = "SubstitutedModel"
			FieldNames(12) = "DripManufacturer"
			
			Set globaldocconfigcenter = HSGetProfileDocument(ConfigCenter, "GlobalSettings")
			Set globaldocehslive = HSGetProfileDocument(HealthSpace, "GlobalSettings")
			
			Forall FieldName In FieldNames
				Set DestField = globaldocehslive.GetFirstItem( FieldName )
				If DestField Is Nothing Then
					Set DestField = globaldocehslive.AppendItemValue( FieldName, "" )
				End If
				Set SourceField = globaldocconfigcenter.GetFirstItem( FieldName )
				If Not SourceField Is Nothing Then
					If SourceField.Text <> DestField.Text Then
						Call globaldocehslive.ReplaceItemValue( FieldName, SourceField.Values )
						bSave = True
					End If 'source<>dest
				End If 'source nothing
			End Forall
			
			If bSave = True Then
				globaldocehslive.configdblastupdated = Now()
				Call globaldocehslive.save(True,True)
			End If
			
		End If 'configcenter.open
	End If 'reader?
End Sub

'++LotusScript Development Environment:2:1:GetDBID:1:8
Function GetDBID( ID As String ) As String
	Dim Session As New NotesSession
	Dim HealthSpace As NotesDatabase
	Dim MasterSettings As NotesDocument
	Dim idItem As NotesItem
	
	Set HealthSpace = Session.CurrentDatabase
	Set MasterSettings = HSGetProfileDocument(HealthSpace, "MasterSettings" )
	If Not MasterSettings Is Nothing Then
		Set idItem = MasterSettings.GetFirstItem( ID & "ID" )
		If idItem Is Nothing Then
			GetDBID = ""
		Else
			GetDBID = Left( idItem.Values(0), 8 ) & Right( idItem.Values(0), 8 )
		End If
	End If
End Function

'++LotusScript Development Environment:2:1:TempVendID:1:8
Function TempVendID As String
	TempVendID = GetDBID( "TempVend" )
End Function

'++LotusScript Development Environment:2:1:BugReportingID:1:8
Function BugReportingID
	BugReportingID = GetDBID( "BugReporting" )
End Function

'++LotusScript Development Environment:2:1:DBDesign:1:8
Function DBDesign() As String
	Dim session As New NotesSession
	Dim db As NotesDatabase
	Dim masterdoc As NotesDocument
	
%REM
	Gets set to the template name as it is in properties (Database file is a master template)
	HS EHS WI and anything that inherits from it = Wisconsin

HS EHS Washington	:	WA
HS EHS Ohio		:	Ohio
HS EHS Colorado		:	Colorado
HS EHS Erie		:	Erie
HS EHS WI		:	Wisconsin

HS Violations Washington:	WA Violations
HS Violations Ohio	:	Ohio Violations
HS Violations Colorado	:	Colorado Violations
HS Violations Erie	:	Erie Violations
HS Violations Wisconsin	:	Wisconsin Violations
%ENDREM
	
	DBDesign = ""
	
	Set db = session.CurrentDatabase
	Set masterdoc = HSGetProfileDocument(db, "MasterSettings" )
	
	If masterdoc.HasItem( "DBDesign" ) Then		
		DBDesign = masterdoc.GetItemValue( "DBDesign" )(0) 				
	End If	
	
End Function

'++LotusScript Development Environment:2:2:Check_TempVendor:1:8
Sub Check_TempVendor
	
	Dim Session As New NotesSession
	Dim HealthSpace As NotesDatabase	
	Dim config As NotesDocument
	
	If Databases_Servers_Enabled Then 'Temp Vendors is STUPID without a server
		If Databases_TempVendor_Enabled Then
			Set HealthSpace = Session.CurrentDatabase
			Set config = Healthspace.GetProfileDocument( "PersonalSettings", session.UserName )
			'before we look for temp vendor db, check if this user has asked to skip this test
			If config.SkipTV_Test(0) <> "Yes" Then
				GetDependantDatabase "TempVend", "Temporary Food Vendors" 
			End If 'personal skip		
		End If 'tv on
	End If 'servers disabled
	
End Sub

'++LotusScript Development Environment:2:1:GetDependantDatabase:1:8
Function GetDependantDatabase( SettingsField As String, DependantDBName As String ) As NotesDatabase
	
	'opens a database on the same server (or local) as EHS using the replica ID from MasterSettings
	'	if things go wrong, msgs are displayed, and if possible, a server replica will be used
	'	and optionally replicated to the local machine
	
	Dim Workspace As New NotesUIWorkspace
	Dim Session As New NotesSession
	Dim HealthSpace As NotesDatabase
	Dim ConfigCenter As New NotesDatabase( "", "" )
	Dim Msg As String
	
	Set HealthSpace = Session.CurrentDatabase
	'don't bother for reader access
	If HealthSpace.CurrentAccessLevel <> ACLLEVEL_READER Then
		If GetDBID( SettingsField ) = "" Then
			Msgbox "It appears the database '" & HealthSpace.Title _
			& "' is not fully initalized. MasterSetting has no Database ID for the " _
			& DependantDBName & ". Please call HealthSpace Support." _
			, 48, "Configuration error"
		Elseif Not ConfigCenter.OpenByReplicaID( HealthSpace.Server, GetDBID( SettingsField ) ) Then
			If HealthSpace.Server = "" Then
				Msg = "A Local replica of " & DependantDBName & " "
			Else
				Msg = DependantDBName & " on " & HealthSpace.Server
			End If
			Msg = Msg & " could not be found. "
			If Not Databases_Servers_Enabled Then 'note, this should not happen for TempVend, ServerEnabled is checked in the wrapper sub
				'SHUT THEM DOWN!
				If HealthSpace.Server = "" Then 'normal message
					Msgbox Msg _
					& "Database '" & HealthSpace.Title & "' will be unable to function correctly." _ 
					& Chr(10) & "Please call HealthSpace to obtain a replica of " _
					& DependantDBName & "."_ 
					, 48, "Configuration error" 
				Else 'wow
					Msgbox "Database '" & HealthSpace.Title _
					& "' is configured for serverless access, but you are on " & HealthSpace.Server & "."_
					& Chr(10) & "Please call HealthSpace to have this database reconfigured," _ 
					& Chr(10) & "or have a replica of " & DependantDBName _
					& " added to " & HealthSpace.Server & "."_
					, 48, "This is Strange!"  
				End If
			Elseif FavouriteServer() = "" Then
				'SHUT THEM DOWN!
				Msgbox "No Servers specified. " & Msg _
				& "Database '" & HealthSpace.Title & "' will be unable to function correctly." _ 
				& Chr(10) & "Please call HealthSpace to have this database reconfigured." _ 
				, 48, "Configuration error" 
			Else
				If Not SkipGetReplicaQuestion( SettingsField, DependantDBName ) Then
					Msg = Msg & "Would you like to get a new replica from " & FavouriteServer() & "?"
					'not found, can we go to server???
					If Workspace.Prompt( PROMPT_YESNO, "Create New Replica", Msg ) Then
						'user said yes
						Dim MasterConfigCenter As New NotesDatabase( "", "" )
						If MasterConfigCenter.OpenByReplicaID( FavouriteServer(), ViolationsID ) Then
							Set ConfigCenter = MasterConfigCenter.CreateReplica( HealthSpace.Server, MasterConfigCenter.FileName )
							'create workspace icon
							If ConfigCenter.FilePath <> "" Then Call Workspace.AddDatabase( "", ConfigCenter.FilePath )
						Else
							'SHUT THEM DOWN!
							Msgbox "Unable to locate " & DependantDBName & " on " & FavouriteServer() _
							& ". Database '" & HealthSpace.Title & "' will be unable to function correctly." _
							, 48, "Configuration error"
						End If
					Else 'they said No, 
						If Not ConfigCenter.OpenByReplicaID( FavouriteServer(), ViolationsID ) Then
							'Rats! not on server, or no server available
							'SHUT THEM DOWN!
							Msgbox DependantDBName & " cannot be found on " & FavouriteServer() _
							& ". Database '" & HealthSpace.Title & "' will be unable to function correctly." _
							, 48, "Configuration error"
						Else
							'continuing with config center opened on server
						End If
						RememberGetReplicaAnswer SettingsField 
					End If 'prompt create replica?
				End If 'checkchoice
			End If 'servers enabled, favourite?blank,
		End If 'initialized, and exists, all is fine
	End If 'reader?
	
	Set GetDependantDatabase = ConfigCenter
	
End Function

'++LotusScript Development Environment:2:1:SkipGetReplicaQuestion:1:8
Private Function SkipGetReplicaQuestion( Byval Typ As String, prettype As String ) As Boolean
	
	'has a slippery side effect of asking for confirmation when the existing "Yes" is old
	
	Dim Workspace As New NotesUIWorkspace
	Dim session As New NotesSession
	Dim personalconfig As NotesDocument	
	Dim db As NotesDatabase
	Dim skip As String
	Dim item As NotesItem
	Dim msg As String
	Dim dtmod As NotesDateTime
	Dim dtlastyear As New NotesDateTime( Now )
'	dtlastyear.AdjustYear -1
	Dim saveage As String
	
	Set db = Workspace.CurrentDatabase.Database
	Set personalconfig = db.GetProfileDocument( "PersonalSettings", session.UserName )
	If typ = "TempVend" Then typ = "TV"
	Set item = personalconfig.GetFirstItem( "Skip" & Typ & "_Test" )
	If item Is Nothing Then Set item = personalconfig.ReplaceItemValue( "Skip" & Typ & "_Test", "" )
	skip = item.Text
	
	If skip = "Yes" Then 'has the decision to skip the test expired?
		Set dtmod = New NotesDateTime( item.LastModified )
		saveage = age( dtlastyear.TimeDifferenceDouble( dtmod ))
		dtlastyear.AdjustYear -1
		If dtlastyear.TimeDifferenceDouble( dtmod ) > 0 Then
			msg = "On " & Format( dtmod.LSLocalTime, "ddmmmyyyy" ) _
			& " you asked not to be reminded to make a local replica of " & prettype _
			& ". That was " & saveage & " ago. Is that still your preference?"
			If Workspace.Prompt( PROMPT_YESNO, "Have things changed?", Msg ) Then
				'they said no
			Else
				skip = "No"
				item.Text = "No"
				personalconfig.save True, True
			End If 'prompt
		End If 'timediff
	End If 'said skip
	
	SkipGetReplicaQuestion = skip = "Yes"
	
End Function

'++LotusScript Development Environment:2:1:age:1:8
Private Function age( Byval t As Double ) As String
	t = Abs(t)
	If t < 60 Then 
		age = Cint(t) & " seconds"
	Else
		t = t / 60
		If t < 60 Then 
			age = Cint(t) & " minutes"
		Else
			t = t / 60
			If t < 24 Then 
				age = Cint(t) & " hours"
			Else
				t = t / 24
				If t < 7 Then 
					age = Cint(t) & " days"
				Else
					If t < 30 Then 
						age = Cint( (t *10)/ 7 )/10 & " weeks"
					Else
						t = t / 30
						If t < 12 Then 
							age = Cint(t*10)/10 & " months"
						Else
							t=(t*30)/365
							age = Cint(t*10)/10 & " years"
						End If
					End If
				End If
			End If
		End If
	End If
End Function 

'++LotusScript Development Environment:2:2:RememberGetReplicaAnswer:1:8
Private Sub RememberGetReplicaAnswer( Byval Typ As String )
	
	'Let's remember their answer when they say No
	'I'm not happy with remembering it only by username. the answer could be different 
	'	for desktop and laptop! Can we consider using MAC? 
	'Issue#2 we only remember TV answers. should we remember configctr answers?
	
	Dim Workspace As New NotesUIWorkspace
	Dim session As New NotesSession
	Dim personalconfig As NotesDocument	
	Dim db As NotesDatabase
	Dim oldchoice As String
	Dim newchoice As String
	Dim item As NotesItem
	
	Set db = Workspace.CurrentDatabase.Database
	Set personalconfig = db.GetProfileDocument( "PersonalSettings", session.UserName )
	If typ = "TempVend" Then typ = "TV"
	Set item = personalconfig.GetFirstItem( "Skip" & Typ & "_Test" )
	If item Is Nothing Then Set item = personalconfig.ReplaceItemValue( "Skip" & Typ & "_Test", "" )
	oldchoice = item.Text
	If oldchoice = "" Then oldchoice = "0"
	If Isnumeric( oldchoice ) And Typ <> "TV" Then 
		newchoice = Format( Cint( oldchoice ) + 1 )
		If Cint( newchoice ) > 3 Then newchoice = "Ask"
	Else
		newchoice = "Ask"
	End If	
	
	If newchoice = "Ask" Then
		If Workspace.Prompt( PROMPT_YESNO, "Ask again?", _
		"Do you want to be prompted to create a local replica next time you log in?") Then
			newchoice = "No"
		Else
			newchoice = "Yes"
		End If
	End If
	
	item.Values = newchoice
	If newchoice <> oldchoice Then personalconfig.save True, True
	
End Sub